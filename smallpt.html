<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <title>SmallPT WebGL</title>
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <link crossorigin="anonymous" href="https://cdn.jsdelivr.net/npm/bootstrap@5.0.0-beta1/dist/css/bootstrap.min.css"
          integrity="sha384-giJF6kkoqNQ00vy+HMDP7azOuL0xtbfIcaT9wjKHr8RbDVddVHyTfAAsrekwKmP1" rel="stylesheet">
    <style>
        html, body {
            height: 100%;
        }

        body {
            color: aliceblue;
            background-color: #123;
            overflow: hidden;
        }

        .fill {
            min-height: 100%;
            height: 100%;
        }

        .container {
            display: flex;
            flex-direction: column;
            justify-content: center;
            align-items: center;
        }
    </style>
</head>
<body>
<div class="container fill">
    <div class="row">
        <div class="col-12">
            <span class="label label-default">Ready</span>
        </div>
    </div>
    <div class="row">
        <div class="col-12">
            <canvas width="1024" height="768"></canvas>
        </div>
    </div>
    <div class="row">
        <div class="col-12">
            <button type="button" class="btn btn-danger">Render</button>
        </div>
    </div>
</div>

<script id="vs" type="x-shader/x-vertex">#version 300 es
in vec2 position;
out vec2 fragCoord;

void main() {
    fragCoord = position;
    gl_Position = vec4(position.x, position.y, 0.0, 1.0);
}
</script>

<script id="fs" type="x-shader/x-fragment">#version 300 es
precision highp float;

#define MAXDEPTH 4

#define PI 3.14159265359
#define DIFF 0
#define SPEC 1
#define REFR 2

float seed = 0.4126984126984126984;
float rand() {
    seed += 0.5743312;
    return fract(sin(seed) * 43758.5453123);
}

struct Ray { vec3 o, d; };

struct Sphere {
    float r;
    vec3 p, e, c;
    int refl;
};

Sphere lightSourceVolume = Sphere(20., vec3(50., 81.6, 81.6), vec3(12.), vec3(0.), DIFF);
Sphere spheres[9];

float intersect(Sphere s, Ray r) {
    float epsilon = 0.0001;
    vec3 op = s.p - r.o;
    float b = dot(op, r.d);
    float det = b * b - dot(op, op) + s.r * s.r;
    if (det < 0.0) return 0.0;
    det = sqrt(det);
    float t1 = b - det;
    float t2 = b + det;
    return t1 > epsilon ? t1 : t2 > epsilon ? t2 : 0.0;
}

int intersect(Ray r, out float t, out Sphere s, int avoid) {
    int id = -1;
    t = 10000.0;
    s = spheres[0];
    for (int i = 0; i < spheres.length(); ++i) {
        Sphere sphere = spheres[i];
        float d = intersect(sphere, r);
        if (i != avoid && d > 0.0 && d < t) {
            t = d;
            id = i;
            s = sphere;
        }
    }
    return id;
}

vec3 jitter(vec3 d, float phi, float sina, float cosa) {
    vec3 w = normalize(d);
    vec3 u = normalize(cross(w.yzx, w));
    vec3 v = cross(w, u);
    return (u * cos(phi) + v * sin(phi)) * sina + w * cosa;
}

vec3 radiance(Ray r) {
    vec3 acc = vec3(0.);
    vec3 mask = vec3(1.);
    int id = -1;
    for (int depth = 0; depth < MAXDEPTH; ++depth) {
        float t;
        Sphere obj;
        if ((id = intersect(r, t, obj, id)) < 0) break;
        vec3 x = t * r.d + r.o;
        vec3 n = normalize(x - obj.p), nl = n * sign(-dot(n, r.d));

        if (obj.refl == DIFF) {
            float r2 = rand();
            vec3 d = jitter(nl, 2.0 * PI * rand(), sqrt(r2), sqrt(1.0 - r2));
            vec3 e = vec3(0.0);
            Sphere s = lightSourceVolume;
            int i = 8;

            vec3 l0 = s.p - x;
            float cos_a_max = sqrt(1. - clamp(s.r * s.r / dot(l0, l0), 0.0, 1.0));
            float cosa = mix(cos_a_max, 1.0, rand());
            vec3 l = jitter(l0, 2.0 * PI * rand(), sqrt(1.0 - cosa * cosa), cosa);

            if (intersect(Ray(x, l), t, s, id) == i) {
                float omega = 2.0 * PI * (1.0 - cos_a_max);
                e += (s.e * clamp(dot(l, n), 0.0, 1.0) * omega) / PI;
            }
            float E = 1.0;
            acc += mask * obj.e * E + mask * obj.c * e;
            mask *= obj.c;
            r = Ray(x, d);
        } else if (obj.refl == SPEC) {
            acc += mask * obj.e;
            mask *= obj.c;
            r = Ray(x, reflect(r.d, n));
        } else {
            float a = dot(n, r.d);
            float ddn = abs(a);
            float nc=1.0;
            float nt=1.5;
            float nnt = mix(nc / nt, nt / nc, float(a > 0.0));
            float cos2t = 1.0 - nnt * nnt * (1.0 - ddn * ddn);
            r = Ray(x, reflect(r.d, n));
            if (cos2t > 0.0) {
                vec3 tdir = normalize(r.d*nnt + sign(a)*n*(ddn*nnt+sqrt(cos2t)));
                float r0 = (nt-nc) * (nt-nc) / ((nt+nc) * (nt+nc));
                float c = 1.-mix(ddn, dot(tdir, n), float(a > 0.0));
                float re = r0 + (1.0 - r0) * c * c * c * c * c;
                float p = 0.25 + 0.5 * re;
                float rp = re / p;
                float tp = (1.0 - re) / (1.0 - p);
                if (rand() < p) {
                    mask *= rp;
                } else {
                    mask *= obj.c * tp;
                    r = Ray(x, tdir);
                }
            }
        }
    }
    return acc;
}

in vec2 fragCoord;
out vec4 fragColor;
uniform float iTime;

void main() {
    spheres[0] = Sphere(100000.0, vec3(100001.0, 40.8, 81.6), vec3(0.0), vec3(0.75, 0.25, 0.25), DIFF);
    spheres[1] = Sphere(100000.0, vec3(-99901.0, 40.8, 81.6), vec3(0.0), vec3(0.25, 0.25, 0.75), DIFF);
    spheres[2] = Sphere(100000.0, vec3(50.0, 40.8, 100000.0), vec3(0.0), vec3(0.75, 0.75, 0.75), DIFF);
    spheres[3] = Sphere(100000.0, vec3(50.0, 40.8, -99830.0), vec3(0.0), vec3(0.0), DIFF);
    spheres[4] = Sphere(100000.0, vec3(50.0, 100000.0, 81.6), vec3(0.0), vec3(0.75, 0.75, 0.75), DIFF);
    spheres[5] = Sphere(100000.0, vec3(50.0, -99918.4, 81.6), vec3(0.0), vec3(0.75, 0.75, 0.75), DIFF);
    spheres[6] = Sphere(16.5, vec3(27.0, 16.5, 47.0), vec3(0.0), vec3(0.999, 0.999, 0.999), SPEC);
    spheres[7] = Sphere(16.5, vec3(73.0, 16.5, 78.0), vec3(0.0), vec3(0.700, 0.999, 0.900), REFR);
    spheres[8] = Sphere(600.0, vec3(50.0, 681.33, 81.6), vec3(12.0, 12.0, 12.0), vec3(0.0), DIFF);

    vec2 iResolution = vec2(1024.0, 768.0);

    vec2 st = fragCoord.xy / iResolution.xy;
    seed = iTime + iResolution.y * fragCoord.x / iResolution.x + fragCoord.y / iResolution.y;

    vec3 camPos = vec3(50.0, 52.0, 295.6);
    vec3 camDir = normalize(vec3(0.0, -0.042612, -1.0));
    vec3 cx = vec3(iResolution.x * 0.5135 / iResolution.y, 0.0, 0.0);
    vec3 cy = normalize(cross(cx, camDir)) * 0.5135;

    vec3 color = vec3(0.0);
    float x = (fragCoord.x + 1.0) / 2.0 * float(iResolution.x);
    float y = (fragCoord.y + 1.0) / 2.0 * float(iResolution.y);
    vec3 r = vec3(0.0);
    for (int s = 0; s < 256 / 4; s++)
    {
        float r1 = 2.0 * rand();
        float r2 = 2.0 * rand();
        float dx = r1 < 1.0 ? sqrt(r1) - 1.0 : 1.0 - sqrt(2.0 - r1);
        float dy = r2 < 1.0 ? sqrt(r2) - 1.0 : 1.0 - sqrt(2.0 - r2);
        vec3 tx = cx * (((0.5 + dx) / 2.0 + x) / iResolution.x - 0.5);
        vec3 ty = cy * (((0.5 + dy) / 2.0 + y) / iResolution.y - 0.5);
        vec3 d = camDir + tx + ty;
        color += radiance(Ray(camPos + d * 137.0, normalize(d)));
    }

    fragColor = vec4(color / (256.0 / 4.0), 1.0);
}
</script>
<script>
    function compileShader(gl, shaderSource, shaderType) {
        const shader = gl.createShader(shaderType);
        gl.shaderSource(shader, shaderSource);
        gl.compileShader(shader);
        const success = gl.getShaderParameter(shader, gl.COMPILE_STATUS);
        if (!success) {
            throw "shader compilation failed:" + gl.getShaderInfoLog(shader);
        }
        return shader;
    }

    function compileProgram(gl, vs, fs) {
        const program = gl.createProgram()
        gl.attachShader(program, vs);
        gl.attachShader(program, fs);
        gl.linkProgram(program);
        if (!gl.getProgramParameter(program, gl.LINK_STATUS)) {
            throw "program linking failed:" + gl.getProgramInfoLog(program);
        }
        return program;
    }

    function createBuffer(data) {
        const buffer = gl.createBuffer();
        gl.bindBuffer(gl.ARRAY_BUFFER, buffer);
        gl.bufferData(gl.ARRAY_BUFFER, new Float32Array(data), gl.STATIC_DRAW);
        gl.bindBuffer(gl.ARRAY_BUFFER, null);
        return buffer;
    }

    function displayInfo(query) {
        if (ext != null) {
            document.querySelector("span").innerText = "Awaiting results...";

            let attempts = 0;
            const timerId = setInterval(() => {
                const available = gl.getQueryParameter(query, gl.QUERY_RESULT_AVAILABLE);
                const disjoint = gl.getParameter(ext.GPU_DISJOINT_EXT);
                if (available && !disjoint) {
                    const nanos = gl.getQueryParameter(query, gl.QUERY_RESULT);
                    document.querySelector("span").innerText = `Elapsed: ${(nanos / 1000000000.0).toFixed(2)} second(s)`;
                    clearInterval(timerId);
                } else {
                    document.querySelector("span").innerText = "Awaiting results...";
                }
                if (++attempts > 16) {
                    clearInterval(timerId)
                }
            }, 1000);
        } else {
            document.querySelector("span").innerText = "WebGL timer query extension not available!"
        }
    }

    const canvas = document.querySelector("canvas");
    const gl = canvas.getContext("webgl2");
    const ext = gl.getExtension('EXT_disjoint_timer_query_webgl2');
    const vertexShader = compileShader(gl, document.getElementById("vs").text, gl.VERTEX_SHADER);
    const fragmentShader = compileShader(gl, document.getElementById("fs").text, gl.FRAGMENT_SHADER);
    const program = compileProgram(gl, vertexShader, fragmentShader);
    gl.useProgram(program);

    const buffer = createBuffer([-1.0, -1.0, -1.0, 1.0, 1.0, 1.0, 1.0, 1.0, 1.0, -1.0, -1.0, -1.0]);
    gl.bindBuffer(gl.ARRAY_BUFFER, buffer);

    const position = gl.getAttribLocation(program, 'position');
    const time = gl.getUniformLocation(program, 'iTime');
    gl.enableVertexAttribArray(position);
    gl.vertexAttribPointer(position, 2, gl.FLOAT, gl.FALSE, 0, 0);
    gl.clearColor(0.0, 0.0, 0.0, 1.0);
    gl.clear(gl.COLOR_BUFFER_BIT);

    document.querySelector("button").addEventListener("click", (e) => {
        document.querySelector("span").innerText = "Rendering...";
        e.target.disabled = true;
        setTimeout(() => {
            gl.uniform1f(time, performance.now() / 1000000000.0);
            const query = gl.createQuery();
            ext && gl.beginQuery(ext.TIME_ELAPSED_EXT, query);
            gl.drawArrays(gl.TRIANGLES, 0, 6);
            ext && gl.endQuery(ext.TIME_ELAPSED_EXT);
            displayInfo(query);
            e.target.disabled = false;
        }, 500);
    });

</script>
</body>
</html>
